/*********************************************************************
This is an example for our Monochrome OLEDs based on SSD1306 drivers

  Pick one up today in the adafruit shop!
  ------> http://www.adafruit.com/category/63_98

This example is for a 128x64 size display using I2C to communicate
3 pins are required to interface (2 I2C and one reset)

Adafruit invests time and resources providing this open source code, 
please support Adafruit and open-source hardware by purchasing 
products from Adafruit!

Written by Limor Fried/Ladyada  for Adafruit Industries.  
BSD license, check license.txt for more information
All text above, and the splash screen must be included in any redistribution
*********************************************************************
MODIFICATION FOR MADE
*********************************************************************/

// --------------------------
// How to connect it:
// GND --> GND
// VCC --> VCC
// SCL --> A4
// SDA --> A5
// --------------------------

#include <SPI.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define OLED_RESET 4
Adafruit_SSD1306 display(OLED_RESET);

#define NUMFLAKES 10
#define XPOS 0
#define YPOS 1
#define DELTAY 2


static unsigned char PROGMEM flappybird_frame_1[] = { 0x03, 0xF0, 0x0C, 0x48, 0x10, 0x84, 0x78, 0x8A, 0x84, 0x8A, 0x82, 0x42, 0x82, 0x3E, 0x44, 0x41,0x38, 0xBE, 0x20, 0x41, 0x18, 0x3E, 0x07, 0xC0 };
  
static const unsigned char PROGMEM MADElogo [] = {
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1E, 0x00, 0x00, 0x00, 0x00, 0x00,
0x07, 0xF8, 0x0F, 0xF0, 0x00, 0x00, 0x7E, 0x00, 0x00, 0x00, 0x1E, 0x01, 0xFF, 0xE0, 0x00, 0x00,
0x1F, 0xFF, 0xBF, 0xFE, 0x00, 0x00, 0xFF, 0xC0, 0x3F, 0xFF, 0xFE, 0x0F, 0xFF, 0xFE, 0x00, 0x00,
0x1F, 0xFF, 0xFF, 0xFF, 0x00, 0x00, 0x7F, 0xE0, 0xFF, 0xFF, 0xFE, 0x1F, 0xFF, 0xFF, 0x00, 0x00,
0x3C, 0x01, 0xF0, 0x1F, 0x80, 0x00, 0x01, 0xF0, 0xF8, 0x00, 0x1E, 0x3C, 0x00, 0x3F, 0x80, 0x00,
0x38, 0x01, 0xE0, 0x03, 0x80, 0x00, 0x00, 0x79, 0xE0, 0x00, 0x1E, 0x38, 0x00, 0x07, 0x80, 0x00,
0x38, 0x01, 0xE0, 0x03, 0x80, 0xFF, 0xFF, 0xF9, 0xE0, 0x00, 0x1E, 0x78, 0x00, 0x07, 0x80, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x83, 0xFF, 0xFF, 0xF9, 0xE0, 0x00, 0x1E, 0x78, 0x00, 0x07, 0x80, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x87, 0xFF, 0xFF, 0xF9, 0xE0, 0x00, 0x1E, 0x7F, 0xFF, 0xFF, 0x80, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x8F, 0x00, 0x00, 0x79, 0xE0, 0x00, 0x1E, 0x7F, 0xFF, 0xFF, 0x80, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x8F, 0x00, 0x00, 0x79, 0xE0, 0x00, 0x1E, 0x7F, 0xFF, 0xFF, 0x00, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x8F, 0x00, 0x00, 0x79, 0xE0, 0x00, 0x1E, 0x7C, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x8F, 0x00, 0x00, 0x79, 0xE0, 0x00, 0x1E, 0x78, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x8F, 0x00, 0x00, 0x71, 0xE0, 0x00, 0x1E, 0x7C, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x8F, 0x80, 0x00, 0xF1, 0xF0, 0x00, 0x3C, 0x3F, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0xE0, 0x03, 0x87, 0xFF, 0xFF, 0xF0, 0xFF, 0xFF, 0xF8, 0x1F, 0xFF, 0xFF, 0xFF, 0xFF,
0x38, 0x00, 0xE0, 0x03, 0x83, 0xFF, 0xFF, 0xE0, 0x7F, 0xFF, 0xF8, 0x0F, 0xFF, 0xFF, 0xFF, 0xFF,
0x38, 0x00, 0xE0, 0x03, 0x80, 0xFF, 0xFF, 0x00, 0x1F, 0xFF, 0x80, 0x03, 0xFF, 0xFF, 0xFF, 0xFF,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0xE0, 0x07, 0xF8, 0x07, 0xF8, 0x00, 0xC0, 0x1C, 0x18, 0x1C, 0x03, 0x80, 0x1F, 0x80,
0x38, 0x00, 0xF0, 0x07, 0xFE, 0x07, 0xFF, 0x00, 0xC0, 0x1C, 0x18, 0x1E, 0x03, 0x80, 0xFF, 0xE0,
0x38, 0x01, 0xF0, 0x06, 0x0F, 0x06, 0x07, 0x80, 0xC0, 0x1C, 0x18, 0x1E, 0x03, 0x81, 0xE0, 0xF0,
0x38, 0x01, 0xF0, 0x06, 0x03, 0x06, 0x01, 0xC0, 0xC0, 0x1C, 0x18, 0x1F, 0x03, 0x83, 0x80, 0x38,
0x38, 0x01, 0xB8, 0x06, 0x03, 0x06, 0x00, 0xE0, 0xC0, 0x1C, 0x18, 0x1B, 0x03, 0x83, 0x80, 0x38,
0x38, 0x03, 0x98, 0x06, 0x03, 0x06, 0x00, 0xE0, 0xC0, 0x1C, 0x18, 0x1B, 0x83, 0x83, 0x00, 0x18,
0x38, 0x03, 0x18, 0x06, 0x07, 0x06, 0x00, 0x60, 0xC0, 0x1C, 0x18, 0x19, 0xC3, 0x87, 0x00, 0x1C,
0x38, 0x03, 0x1C, 0x06, 0x0F, 0x06, 0x00, 0x60, 0xC0, 0x1C, 0x18, 0x19, 0xC3, 0x87, 0x00, 0x1C,
0x38, 0x07, 0x0C, 0x07, 0xFC, 0x06, 0x00, 0x70, 0xC0, 0x1C, 0x18, 0x18, 0xE3, 0x87, 0x00, 0x1C,
0x38, 0x06, 0x0E, 0x07, 0xF8, 0x06, 0x00, 0x70, 0xC0, 0x1C, 0x18, 0x18, 0x63, 0x87, 0x00, 0x1C,
0x38, 0x0E, 0x0E, 0x06, 0x1C, 0x06, 0x00, 0x70, 0xC0, 0x1C, 0x18, 0x18, 0x73, 0x87, 0x00, 0x1C,
0x38, 0x0C, 0x06, 0x06, 0x0E, 0x06, 0x00, 0x60, 0xC0, 0x1C, 0x18, 0x18, 0x33, 0x87, 0x00, 0x1C,
0x38, 0x0F, 0xFF, 0x06, 0x06, 0x06, 0x00, 0x60, 0xC0, 0x1C, 0x18, 0x18, 0x3B, 0x87, 0x00, 0x1C,
0x38, 0x1F, 0xFF, 0x06, 0x07, 0x06, 0x00, 0xE0, 0xC0, 0x1C, 0x18, 0x18, 0x1B, 0x87, 0x00, 0x18,
0x38, 0x18, 0x03, 0x06, 0x03, 0x06, 0x00, 0xE0, 0xC0, 0x18, 0x18, 0x18, 0x1F, 0x83, 0x80, 0x38,
0x38, 0x18, 0x03, 0x86, 0x03, 0x86, 0x01, 0xC0, 0xE0, 0x38, 0x18, 0x18, 0x0F, 0x83, 0x80, 0x70,
0x38, 0x38, 0x03, 0x86, 0x03, 0x86, 0x07, 0x80, 0x70, 0x70, 0x18, 0x18, 0x0F, 0x81, 0xE0, 0xF0,
0x38, 0x30, 0x01, 0x86, 0x01, 0x87, 0xFF, 0x00, 0x3F, 0xE0, 0x18, 0x18, 0x07, 0x80, 0xFF, 0xC0,
0x38, 0x30, 0x01, 0xC6, 0x01, 0xC7, 0xF8, 0x00, 0x0F, 0x80, 0x18, 0x18, 0x03, 0x80, 0x3F, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
0x38, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};

#if (SSD1306_LCDHEIGHT != 64)
#error("Height incorrect, please fix Adafruit_SSD1306.h!");
#endif

void setup()   {                
  Serial.begin(9600);

  // by default, we'll generate the high voltage from the 3.3v line internally! (neat!)
  display.begin(SSD1306_SWITCHCAPVCC, 0x3C);  // initialize with the I2C addr 0x3D (for the 128x64)
  // init done

  // Clear the buffer. As we are using adafruit lib buffer contains it's logo
  display.clearDisplay();

  // Load MADElogo to buffer
  display.drawBitmap(0, 0,  MADElogo, 128, 64, 1);
  
  // Show image buffer on the display hardware.
  // Since the buffer is intialized with an Adafruit splashscreen
  // internally, this will display the splashscreen.
  display.display();
  delay(2000);

  // Clear the buffer.
  display.clearDisplay();

  // draw a single pixel
  display.drawPixel(10, 10, WHITE);
  // Show the display buffer on the hardware.
  // NOTE: You _must_ call display after making any drawing commands
  // to make them visible on the display hardware!
  display.display();
  delay(2000);
  display.clearDisplay();

  // draw many lines
  testdrawline();
  display.display();
  delay(2000);
  display.clearDisplay();

  // draw rectangles
  testdrawrect();
  display.display();
  delay(2000);
  display.clearDisplay();

  // draw multiple rectangles
  testfillrect();
  display.display();
  delay(2000);
  display.clearDisplay();

  // draw mulitple circles
  testdrawcircle();
  display.display();
  delay(2000);
  display.clearDisplay();

  // draw a white circle, 10 pixel radius
  display.fillCircle(display.width()/2, display.height()/2, 10, WHITE);
  display.display();
  delay(2000);
  display.clearDisplay();

  testdrawroundrect();
  delay(2000);
  display.clearDisplay();

  testfillroundrect();
  delay(2000);
  display.clearDisplay();

  testdrawtriangle();
  delay(2000);
  display.clearDisplay();
   
  testfilltriangle();
  delay(2000);
  display.clearDisplay();

  // draw the first ~12 characters in the font
  testdrawchar();
  display.display();
  delay(2000);
  display.clearDisplay();

  // draw scrolling text
  testscrolltext();
  delay(2000);
  display.clearDisplay();

  // text display tests
  display.setTextSize(1);
  display.setTextColor(WHITE);
  display.setCursor(0,0);
  display.println("Hello, world!");
  display.setTextColor(BLACK, WHITE); // 'inverted' text
  display.println(3.141592);
  display.setTextSize(2);
  display.setTextColor(WHITE);
  display.print("0x"); display.println(0xDEADBEEF, HEX);
  display.display();
  delay(2000);

  // miniature bitmap display
  display.clearDisplay();
  display.drawBitmap(30, 16,  flappybird_frame_1, 16, 16, 1);
  display.display();

  // invert the display
  display.invertDisplay(true);
  delay(1000); 
  display.invertDisplay(false);
  delay(1000); 

  // draw a bitmap icon and 'animate' movement
  testdrawbitmap(flappybird_frame_1, 16, 16);
}


void loop() {
  
}


void testdrawbitmap(const uint8_t *bitmap, uint8_t w, uint8_t h) {
  uint8_t icons[NUMFLAKES][3];
 
  // initialize
  for (uint8_t f=0; f< NUMFLAKES; f++) {
    icons[f][XPOS] = random(display.width());
    icons[f][YPOS] = 0;
    icons[f][DELTAY] = random(5) + 1;
    
    Serial.print("x: ");
    Serial.print(icons[f][XPOS], DEC);
    Serial.print(" y: ");
    Serial.print(icons[f][YPOS], DEC);
    Serial.print(" dy: ");
    Serial.println(icons[f][DELTAY], DEC);
  }

  while (1) {
    // draw each icon
    for (uint8_t f=0; f< NUMFLAKES; f++) {
      display.drawBitmap(icons[f][XPOS], icons[f][YPOS], flappybird_frame_1, w, h, WHITE);
    }
    display.display();
    delay(200);
    
    // then erase it + move it
    for (uint8_t f=0; f< NUMFLAKES; f++) {
      display.drawBitmap(icons[f][XPOS], icons[f][YPOS],  flappybird_frame_1, w, h, BLACK);
      // move it
      icons[f][YPOS] += icons[f][DELTAY];
      // if its gone, reinit
      if (icons[f][YPOS] > display.height()) {
	icons[f][XPOS] = random(display.width());
	icons[f][YPOS] = 0;
	icons[f][DELTAY] = random(5) + 1;
      }
    }
   }
}


void testdrawchar(void) {
  display.setTextSize(1);
  display.setTextColor(WHITE);
  display.setCursor(0,0);

  for (uint8_t i=0; i < 168; i++) {
    if (i == '\n') continue;
    display.write(i);
    if ((i > 0) && (i % 21 == 0))
      display.println();
  }    
  display.display();
}

void testdrawcircle(void) {
  for (int16_t i=0; i<display.height(); i+=2) {
    display.drawCircle(display.width()/2, display.height()/2, i, WHITE);
    display.display();
  }
}

void testfillrect(void) {
  uint8_t color = 1;
  for (int16_t i=0; i<display.height()/2; i+=3) {
    // alternate colors
    display.fillRect(i, i, display.width()-i*2, display.height()-i*2, color%2);
    display.display();
    color++;
  }
}

void testdrawtriangle(void) {
  for (int16_t i=0; i<min(display.width(),display.height())/2; i+=5) {
    display.drawTriangle(display.width()/2, display.height()/2-i,
                     display.width()/2-i, display.height()/2+i,
                     display.width()/2+i, display.height()/2+i, WHITE);
    display.display();
  }
}

void testfilltriangle(void) {
  uint8_t color = WHITE;
  for (int16_t i=min(display.width(),display.height())/2; i>0; i-=5) {
    display.fillTriangle(display.width()/2, display.height()/2-i,
                     display.width()/2-i, display.height()/2+i,
                     display.width()/2+i, display.height()/2+i, WHITE);
    if (color == WHITE) color = BLACK;
    else color = WHITE;
    display.display();
  }
}

void testdrawroundrect(void) {
  for (int16_t i=0; i<display.height()/2-2; i+=2) {
    display.drawRoundRect(i, i, display.width()-2*i, display.height()-2*i, display.height()/4, WHITE);
    display.display();
  }
}

void testfillroundrect(void) {
  uint8_t color = WHITE;
  for (int16_t i=0; i<display.height()/2-2; i+=2) {
    display.fillRoundRect(i, i, display.width()-2*i, display.height()-2*i, display.height()/4, color);
    if (color == WHITE) color = BLACK;
    else color = WHITE;
    display.display();
  }
}
   
void testdrawrect(void) {
  for (int16_t i=0; i<display.height()/2; i+=2) {
    display.drawRect(i, i, display.width()-2*i, display.height()-2*i, WHITE);
    display.display();
  }
}

void testdrawline() {  
  for (int16_t i=0; i<display.width(); i+=4) {
    display.drawLine(0, 0, i, display.height()-1, WHITE);
    display.display();
  }
  for (int16_t i=0; i<display.height(); i+=4) {
    display.drawLine(0, 0, display.width()-1, i, WHITE);
    display.display();
  }
  delay(250);
  
  display.clearDisplay();
  for (int16_t i=0; i<display.width(); i+=4) {
    display.drawLine(0, display.height()-1, i, 0, WHITE);
    display.display();
  }
  for (int16_t i=display.height()-1; i>=0; i-=4) {
    display.drawLine(0, display.height()-1, display.width()-1, i, WHITE);
    display.display();
  }
  delay(250);
  
  display.clearDisplay();
  for (int16_t i=display.width()-1; i>=0; i-=4) {
    display.drawLine(display.width()-1, display.height()-1, i, 0, WHITE);
    display.display();
  }
  for (int16_t i=display.height()-1; i>=0; i-=4) {
    display.drawLine(display.width()-1, display.height()-1, 0, i, WHITE);
    display.display();
  }
  delay(250);

  display.clearDisplay();
  for (int16_t i=0; i<display.height(); i+=4) {
    display.drawLine(display.width()-1, 0, 0, i, WHITE);
    display.display();
  }
  for (int16_t i=0; i<display.width(); i+=4) {
    display.drawLine(display.width()-1, 0, i, display.height()-1, WHITE); 
    display.display();
  }
  delay(250);
}

void testscrolltext(void) {
  display.setTextSize(2);
  display.setTextColor(WHITE);
  display.setCursor(10,0);
  display.clearDisplay();
  display.println("MADE-BCN.org");
  display.display();
 
  display.startscrollright(0x00, 0x0F);
  delay(2000);
  display.stopscroll();
  delay(1000);
  display.startscrollleft(0x00, 0x0F);
  delay(2000);
  display.stopscroll();
  delay(1000);    
  display.startscrolldiagright(0x00, 0x07);
  delay(2000);
  display.startscrolldiagleft(0x00, 0x07);
  delay(2000);
  display.stopscroll();
}
